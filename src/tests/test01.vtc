varnishtest "Test curl vmod"

server s1 {
       rxreq
       expect req.method == GET
       txresp -hdr "Foo: bar" -body "Test"
       accept
       rxreq
       expect req.method == GET
       txresp -hdr "Foo: bar"
       accept
       rxreq
       expect req.method == POST
       expect req.bodylen == 3
       txresp -hdr "Foo: bar"
} -start

varnish v1 -vcl+backend {
	import curl from "${vmod_topbuild}/src/.libs/libvmod_curl.so";
	sub vcl_recv {
	    if (req.http.func == "GET") {
	    	    curl.get("http://${s1_addr}:${s1_port}");
	    	    return (error(404));
	    } elsif (req.http.func == "FETCH") {
	    	    curl.fetch("http://${s1_addr}:${s1_port}");
	    	    return (error(404));
	    } elsif (req.http.func == "POST") {
	    	    curl.post("http://${s1_addr}:${s1_port}", "a=b");
	    	    return (error(404));
	    }
        }

	sub vcl_error {
	    set obj.status = curl.status();
	    set obj.http.foo = curl.header("foo");
	    set obj.http.cl = curl.header("content-length");
	    set obj.http.body = curl.body();
        }
} -start

client c1 {
	txreq -url "/" -hdr "func: GET"
	rxresp
	expect resp.http.foo == "bar"
	expect resp.http.cl == "4"
	expect resp.status == 200
	expect resp.http.body == "Test"
} -run

client c2 {
	txreq -url "/" -hdr "func: FETCH"
	rxresp
	expect resp.http.foo == "bar"
	expect resp.http.cl == "0"
	expect resp.status == 200
	expect resp.http.body == ""
} -run

client c3 {
	txreq -url "/" -hdr "func: POST"
	rxresp
	expect resp.http.foo == "bar"
	expect resp.http.cl == "0"
	expect resp.status == 200
	expect resp.http.body == ""
} -run
